**********************************************************************
*INICIO
**********************************************************************
TP       SUBENTRY BASES=(3,4)
         B     INICIAR
MENU	 WTO   '--------------      MENU     --------------'
         WTO   '1) INGRESAR TRAMO'
         WTO   '2) VER LINEAS'
         WTO   '3) VER CANTIDAD DE ESTACIONES'
         WTO   '4) SALIR'
         WTO   '5) MUESTRO MATRIZ'
         WTO   '-------------------------------------------'
M_OPC    WTOR  'SELECCIONAR OPCION [1-4]',OPCION,,WAITECB
		 WAIT  ECB=WAITECB 
         CLI   OPCION,C'1'
		 BE    TRAMO *INGRESAR TRAMO
		 CLI   OPCION,C'2'
		 BE    LINEAS *VER LINEAS
		 CLI   OPCION,C'3'
		 BE    VEREST *VER ESTACIONES
		 CLI   OPCION,C'4'
		 BE    FIN
		 CLI   OPCION,C'5'
		 BE    MOSTRARM
		 WTO   'OPCION NO VALIDA'
		 B     MENU
FIN      WTO   '=========>    FIN    <========='  
         SUBEXIT

**********************************************************************
* INICIAR VARIABLES
* Y PIDO CANTIDAD DE ESTACIONES
* USO R5 PARA INICIAR EL VECTOR
**********************************************************************
INICIAR  BAL   5,INI_VNL
         B     CANTEST
         
**********************************************************************
* INICIAR VECTOR CON NUMERO DE LINEAS Y SI SE MOSTRO LA ESTACION
* VUELVO CON R5
* USO R6 PARA LA DIRECCION Y R7 PARA CICLAR
**********************************************************************
* CARGO LA MATRIZ DE NUMERO DE LINEA
INI_VNL  LA    6,V_NUMLIN 
         LA    7,20
V_NL     ZAP   0(2,6),=P'0'
         LA    6,2(6)
         BCT   7,V_NL
* CARGO LA MATRIZ DE VISTA
INI_VVI  LA    6,V_VISTO
         LA    7,20
V_VIS    ZAP   0(1,6),=P'0'
         LA    6,1(6)
         BCT   7,V_VIS
         BR    5

**********************************************************************
*CANTIDAD DE ESTACIONES
*USO R5 PARA LLAMAR A LAS RUTINAS DE VALIDACION DE NUMERO Y RANGO
**********************************************************************
CANTEST  WTOR  'Ingrese la cantidad de estaciones?',CANT,,WAITECB
         WAIT  ECB=WAITECB
         BAL   5,VALEST *VERIFICO SI LOS DOS CHAR SON NUMEROS
         BAL   5,RANGO *VALOR EN RANGO
         B     MENU

**********************************************************************
* VALIDO CANTIDAD DE ESTACIONES
* VUELVO CON R5
* USO R6 PARA LA VALIDACION
**********************************************************************
VALEST   LA    6,CANT
         CLC   0(1,6),=C'0'
         BL    NVALID1
         CLC   0(1,6),=C'9'
         BH    NVALID1
         CLC   1(1,6),=C'0'
         BL    NVALID2
         CLC   1(1,6),=C'9'
         BH    NVALID2
DOPAK    PACK  CANTPAK,0(2,6)
VALBK    BR    5
         
NVALID1  WTO   'NUMERO NO VALIDO'
         B     CANTEST
NVALID2  MVC   1(1,6),0(6)
         MVI   0(6),C'0'
         B     DOPAK

***********************************************************************
*        VERIFICO SI EL RANGO ES EL CORRECTO
*        VUELVO CON R5
***********************************************************************  
RANGO    CP    CANTPAK,MAXEST
         BH    NRANGO
         CP    CANTPAK,MINEST
         BL    NRANGO
         BR    5

NRANGO   MVC   VAL_MSG+44(2),MAXESTC
         WTO   MF=(E,VAL_MSG-4)
         B     CANTEST

**********************************************************************
*        INGRESAR TRAMOS
*        USO R5 Y R6
**********************************************************************
TRAMO    WTOR  'INGRESE INICIO DEL TRAMO',TRAM_XYC,,WAITECB
         WAIT  ECB=WAITECB
         BAL   6,VALTRAMO
         CLI   RES_TRC,C'F'
         BE    TRAMO
         MVC   VAL_X,TRAM_XYC
FIN_TRAM WTOR  'INGRESE FIN DEL TRAMO',TRAM_XYC,,WAITECB
         WAIT  ECB=WAITECB
         BAL   6,VALTRAMO
         CLI   RES_TRC,C'F'
         BE    FIN_TRAM
         PACK  DOBLE,TRAM_XYC
         MVC   VAL_Y,TRAM_XYC
*        CARGO LA DIR DE LA MATRIZ
         LA    6,MAT
*        ME MUEVO HASTA LA FILA
         PACK  DOBLE,VAL_X
         SP    DOBLE,=P'1'
         MP    DOBLE,LONGFILP
         CVB   5,DOBLE
         AR    6,5
*        ME MUEVO HASTA LA COLUMNA
         PACK  DOBLE,VAL_Y
         SP    DOBLE,=P'1'
         MP    DOBLE,LONGCOLP
         CVB   5,DOBLE
         AR    6,5
         MVC   0(1,6),=C'1'
*        CARGO LA DIR DE LA MATRIZ PARA LA PARTE INVERTIDA
         LA    6,MAT
*        ME MUEVO HASTA LA FILA
         PACK  DOBLE,VAL_X
         SP    DOBLE,=P'1'
         MP    DOBLE,LONGCOLP
         CVB   5,DOBLE
         AR    6,5
*        ME MUEVO HASTA LA COLUMNA
         PACK  DOBLE,VAL_Y
         SP    DOBLE,=P'1'
         MP    DOBLE,LONGFILP
         CVB   5,DOBLE
         AR    6,5
         MVC   0(1,6),=C'1'
         B     MENU
**********************************************************************
* SUBRUTINA DE VALIDACION
* VUELVO CON R6 Y USO R5 PARA LAS OTRAS RUTINAS
**********************************************************************
VALTRAMO BAL   5,VALTRNUM
         CLI   RES_TRC,C'F'
         BE    VALTRFIN
         PACK  DOBLE,TRAM_XYC
         BAL   5,VALRANGT
VALTRFIN BR    6
**********************************************************************
* VALIDO EL TRAMO INICIAL O FINAL
* VUELVO CON R5
* USO R7 PARA LA VALIDACION
**********************************************************************
VALTRNUM LA    7,TRAM_XYC
         CLC   0(1,7),=C'0'
         BL    NVALTR1
         CLC   0(1,7),=C'9'
         BH    NVALTR1
         CLC   1(1,7),=C'0'
         BL    NVALTR2
         CLC   1(1,7),=C'9'
         BH    NVALTR2
         MVI   RES_TRC,C'T'
VAL_TRBK BR    5
         
NVALTR1  WTO   'NUMERO NO VALIDO'
         MVI   RES_TRC,C'F'
         B     VAL_TRBK
NVALTR2  MVC   1(1,7),0(7)
         MVI   0(7),C'0'
         MVI   RES_TRC,C'T'
         B     VAL_TRBK

**********************************************************************
* VALIDO EL RANGO DEL TRAMO
**********************************************************************
VALRANGT CP    DOBLE,CANTPAK
         BH    NOVAL_R
         CP    DOBLE,MINEST
         BL    NOVAL_R
         MVI   RES_TRC,C'T'
FINVALR  BR    5

NOVAL_R  MVC   VAL_MSG+44(2),CANT
         WTO   MF=(E,VAL_MSG-4)
         MVI   RES_TRC,C'F'
         B     FINVALR

**********************************************************************
*        MUESTRO LINEAS   
**********************************************************************
LINEAS   WTO   'MUESTRO LINEAS'
         B     MENU

**********************************************************************
*        MUESTRO CANTIDAD DE ESTACIONES   
**********************************************************************
VEREST   MVC   TOTESTA+21(2),CANT
         WTO   MF=(E,TOTESTA-4)
         B     MENU

**********************************************************************
*        MUESTRO MATRIZ
**********************************************************************
MOSTRARM WTO   '-------------------------------------------'
         LA    6,20
         LA    5,MAT
ELEMSIG  MVC   ELEM(20),0(5)
         WTO   MF=(E,ELEM-4)
         LA    5,20(5)
         BCT   6,ELEMSIG
         WTO   '-------------------------------------------'
         B     MENU
         
**********************************************************************
*        DEFINICION DE VARIABLES
**********************************************************************
* MAXIMO Y MINIMO DE LAS ESTACIONES
MAXESTC  DC    C'20'
MAXEST   DC    PL2'20'
MINEST   DC    PL2'1'

*CANTIDAD DE ESTACIONES EN CHAR Y EMPAQUETADO
CANT     DS    CL2
CANTPAK  DS    PL2

*PARA LAS OPCIONES DEL MENU
OPCION   DS    CL1

*VECTOR DONDE ALMACENO LA LINEA A LA HORA DE MOSTRARLA
V_NUMLIN DS    20PL2

*VECTOR DONDE GUARDO SI PASE POR ESA LINEA
V_VISTO  DS    20PL1

*MATRIZ DONDE GUARDO LAS RELACIONES DE LAS ESTACIONES
MAT      DC    400CL1'0'

*PARA CONVERTIR EN EMPAQUETADO
DOBLE    DS    FD

*PARA INGRESAR LOS TRAMOS ESTACION INICIAL Y FINAL
VAL_X    DS    CL2
VAL_Y    DS    CL2

*VALORES PARA IR MOVIENDOME EN LA MATRIZ
LONGFILP DC    P'20'
LONGCOLP DC    P'1'

*VALOR DEL TRAMO PARA VALIDAR
TRAM_XYC DS    CL2

*RESULTADO DE LA VALIDACION DE TRAMO
RES_TRC  DS    CL1

*MENSAJE DE VALIDACION
         DC    H'50'
         DC    H'0'
VAL_MSG  DC    C'EL VALOR INGRESADO ESTA FUERA DEL RANGO 1 A 20'

*PARA IMPRIMIR EL TOTAL DE ESTACIONES
         DC    H'29'
         DC    H'0'
TOTESTA  DC    CL25'TOTAL DE ESTACIONES: '

*PARA EL WTOR
WAITECB  DC    F'0'

*SOLO PARA MOSTRAR MATRIZ
VAL      DS    PL1

         DC    H'24'
         DC    H'0'
ELEM     DS    CL20

         END